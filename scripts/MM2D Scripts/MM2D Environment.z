////////////////////////////////////////
//                                    //
//   MM2D Script: Game Environment    //
//                                    //
////////////////////////////////////////

//start MeltableIce
void handleMeltableIce()
{
	for(int i=1;i<=Screen->NumLWeapons();i++)
	{
		lweapon weap = Screen->LoadLWeapon(i);
		if(weap->isValid())
		{
			if((weap->ID==LW_ARROW&&arrowType==ARRTYPE_FIRE)||weap->ID==LW_HOTWATER)
			{
				int combos[4] = {ComboAt(weap->X,weap->Y),ComboAt(weap->X+15,weap->Y),ComboAt(weap->X,weap->Y+15),ComboAt(weap->X+15,weap->Y+15)};
				for(int i=0;i<4;i++)
				{
					if(Screen->ComboI[combos[i]]==CF_SCRIPT_ICE)
					{
						Screen->ComboD[combos[i]]--;
					}
				}
			}
		}
	}
}
//end MeltableIce
//start FreezableWater
void handleFreezableWater()
{
	for(int i=1;i<=Screen->NumLWeapons();i++)
	{
		lweapon weap = Screen->LoadLWeapon(i);
		if(weap->isValid())
		{
			if(weap->ID==LW_ARROW&&arrowType==ARRTYPE_ICE)
			{
				int combos[4] = {ComboAt(weap->X,weap->Y),ComboAt(weap->X+15,weap->Y),ComboAt(weap->X,weap->Y+15),ComboAt(weap->X+15,weap->Y+15)};
				for(int i=0;i<4;i++)
				{
					if(Screen->ComboI[combos[i]]==CF_SCRIPT1&&Screen->ComboT[combos[i]]==CT_WATER)
					{
						Screen->ComboD[combos[i]]++;
					}
				}
			}
		}
	}
}
//end FreezableWater
//start LedgeTooHighFromWater
bool wasOnWaterLedge = false;
void highLedgeFromWater()
{
	//using namespace Misc;
	unless(wasOnWaterLedge)
	{
		if(Hero->Action==LA_SWIMMING || Hero->Action==LA_DIVING)
		{
			if(LinkOnFlagMod(CF_NOWATERLEDGE2,0,LinkMovement[LM_STICKY]*3,0,true)||(form!=FORM_DEKU&&LinkOnFlagMod(CF_NOWATERLEDGE,0,LinkMovement[LM_STICKY]*3,0,true)))
			{
				if(LinkMovement[LM_STICKX]==0)
				{
					if(LinkMovement[LM_STICKY]==1)
					{
						Hero->Dir=DIR_DOWN;
					}
					else if (LinkMovement[LM_STICKY]==-1)
					{
						Hero->Dir=DIR_UP;
					}
				}
				Input->Button[CB_DOWN]=false;
				Input->Button[CB_UP]=false;
				LinkMovement_SetLinkSpeedBoost(0);
			}
			if(LinkOnFlagMod(CF_NOWATERLEDGE2,LinkMovement[LM_STICKX]*3,0,0,true)||(form!=FORM_DEKU&&LinkOnFlagMod(CF_NOWATERLEDGE,LinkMovement[LM_STICKX]*3,0,0,true)))
			{
				if(LinkMovement[LM_STICKY]==0)
				{
					if(LinkMovement[LM_STICKX]==1)
					{
						Hero->Dir=DIR_RIGHT;
					}
					else if (LinkMovement[LM_STICKX]==-1)
					{
						Hero->Dir=DIR_LEFT;
					}
				}
				Input->Button[CB_LEFT]=false;
				Input->Button[CB_RIGHT]=false;
				LinkMovement_SetLinkSpeedBoost(0);
			}
		}
		else
		{
			wasOnWaterLedge=LinkOnFlag(CF_NOWATERLEDGE)||LinkOnFlag(CF_NOWATERLEDGE2);
		}
	}
	else
	{
		wasOnWaterLedge=LinkOnFlag(CF_NOWATERLEDGE)||LinkOnFlag(CF_NOWATERLEDGE2);
	}
}

//end LedgeTooHighFromWater
//start Climb
ffc script climb
{
	void run(int dir, int blockheight)
	{
		int lspdboost = Ceiling(1.5+LinkMovement_GetLinkSpeedBoost());
		int width = this->EffectWidth;
		int height = this->EffectHeight;
		int bottomheight = height;
		if(Hero->BigHitbox)bottomheight-=8;
		while(true)
		{
			if(!isFrozen&&(blockheight>2||form==FORM_DEKU||(blockheight==2&&(form==FORM_HUMAN||form==FORM_GORON||form==FORM_FIERCE_DEITY))))
			{
				if(dir==DIR_UP||dir==4)
				{
					if(Hero->Y>=this->Y-(16+lspdboost))
					{//start inputdown
						if(Hero->Y<=this->Y-16)
						{
							if(Hero->X>=this->X-15)
							{
								if(Hero->X<=this->X+(width-1))
								{
									if(Input->Button[CB_DOWN])
									{
										if(Hero->Y!=this->Y-16)Hero->Y=this->Y-16;
										Hero->Dir=DIR_DOWN;
										if(Hero->X>=this->X+(width-4))
										{
											if(CanWalk(Hero->X,Hero->Y,DIR_RIGHT,Abs(Hero->X-(this->X+width)),false))Hero->X=this->X+width;
										}
										else if(Hero->X<=this->X-12)if(CanWalk(Hero->X,Hero->Y,DIR_LEFT,Abs(Hero->X-this->X-16),false))Hero->X=this->X-16;
									}
									Input->Button[CB_DOWN]=false;
									Input->Press[CB_DOWN]=false;
								}
							}
						}//end
					}
				}
				if(dir==DIR_DOWN||dir==4)
				{
					if(Hero->Y<=this->Y+(bottomheight+lspdboost))
					{//start inputup
						if(Hero->Y>=this->Y+bottomheight)
						{
							if(Hero->X>=this->X-15)
							{
								if(Hero->X<=this->X+(width-1))
								{
									if(Input->Button[CB_UP])
									{
										if(Hero->Y!=this->Y+bottomheight)Hero->Y=this->Y+bottomheight;
										Hero->Dir=DIR_UP;
										if(Hero->X>=this->X+(width-4))
										{
											if(CanWalk(Hero->X,Hero->Y,DIR_RIGHT,Abs(Hero->X-(this->X+width)),false))Hero->X=this->X+width;
										}
										else if(Hero->X<=this->X-12)if(CanWalk(Hero->X,Hero->Y,DIR_LEFT,Abs(Hero->X-this->X-16),false))Hero->X=this->X-16;
									}
									Input->Button[CB_UP]=false;
									Input->Press[CB_UP]=false;
								}
							}
						}//end
					}
				}
				if(dir==DIR_LEFT||dir==4)
				{
					if(Hero->X>=this->X-(16+lspdboost))
					{//start inputright
						if(Hero->X<=this->X-16)
						{
							if(Hero->Y<=this->Y+(bottomheight-1))
							{
								if(Hero->Y>=this->Y-15)
								{
									if(Input->Button[CB_RIGHT])
									{
										if(Hero->X!=this->X-16)Hero->X=this->X-16;
										Hero->Dir=DIR_RIGHT;
										if(Hero->Y>=this->Y+(bottomheight-4))
										{
											if(CanWalk(Hero->X,Hero->Y,DIR_DOWN,Abs(Hero->Y-(this->Y+bottomheight)),false))Hero->Y=this->Y+bottomheight;
										}
										else if(Hero->Y<=this->Y-12)if(CanWalk(Hero->X,Hero->Y,DIR_UP,Abs(Hero->Y-(this->Y-16)),false))Hero->Y=this->Y-16;
									}
									Input->Button[CB_RIGHT]=false;
									Input->Press[CB_RIGHT]=false;
								}
							}
						}//end
					}
				}
				if(dir==DIR_RIGHT||dir==4)
				{
					if(Hero->X<=this->X+(width+lspdboost))
					{//start inputleft
						if(Hero->X>=this->X+width)
						{
							if(Hero->Y>=this->Y-15)
							{
								if(Hero->Y<=this->Y+(bottomheight-1))
								{
									if(Input->Button[CB_LEFT])
									{
										if(Hero->X!=this->X+width)Hero->X=this->X+width;
										Hero->Dir=DIR_LEFT;
										if(Hero->Y>=this->Y+(bottomheight-4))
										{
											if(CanWalk(Hero->X,Hero->Y,DIR_DOWN,Abs(Hero->Y-(this->Y+bottomheight)),false))Hero->Y=this->Y+bottomheight;
										}
										else if(Hero->Y<=this->Y-12)if(CanWalk(Hero->X,Hero->Y,DIR_UP,Abs(Hero->Y-(this->Y-16)),false))Hero->Y=this->Y-16;
									}
									Input->Button[CB_LEFT]=false;
									Input->Press[CB_LEFT]=false;
								}
							}
						}//end
					}
				}
			}
			Waitframe();
		}
	}
}
//end
//start Warp
ffc script warpCustomReturn
{
	//Dirs: 0=Tile, 1=Up, 2=Down, 3=Left, 4=Right
	void run(int dmap, int screen, int X, int Y, int side)
	{
		side-=1;
		switch(side)
		{
			case DIR_UP:
			{
				while(true)
				{
					if(!isFrozen&&Hero->Y<=1&&(Input->Press[CB_UP]||Input->Button[CB_UP]))
					{
						Hero->X=X;
						Hero->Y=Y;
						Hero->PitWarp(dmap,screen);
					}
					Waitframe();
				}
			break;}
			case DIR_DOWN:
			{
				while(true)
				{
					if(!isFrozen&&Hero->Y>=159&&(Input->Press[CB_DOWN]||Input->Button[CB_DOWN]))
					{
						Hero->X=X;
						Hero->Y=Y;
						Hero->PitWarp(dmap,screen);
					}
					Waitframe();
				}
			break;}
			case DIR_LEFT:
			{
				while(true)
				{
					if(!isFrozen&&Hero->X<=1&&(Input->Press[CB_LEFT]||Input->Button[CB_LEFT]))
					{
						Hero->X=X;
						Hero->Y=Y;
						Hero->PitWarp(dmap,screen);
					}
					Waitframe();
				}
			break;}
			case DIR_RIGHT:
			{
				while(true)
				{
					if(!isFrozen&&Hero->X>=239&&(Input->Press[CB_RIGHT]||Input->Button[CB_RIGHT]))
					{
						Hero->X=X;
						Hero->Y=Y;
						Hero->PitWarp(dmap,screen);
					}
					Waitframe();
				}
			break;}
			default:
			{
				while(true)
				{
					if(!isFrozen&&(Abs(Hero->X-this->X)<=14&&Abs(Hero->Y-this->Y)<=14))
					{
						Hero->X=X;
						Hero->Y=Y;
						Hero->PitWarp(dmap,screen);
					}
					Waitframe();
				}
			break;}
		}
	}
}
//end FFC warp
//start SoftSoil
ffc script SoftSoil
{
	void run(int raftDir, int screensUp, bool gskull, int startState, int index)
	{
		this->X = GridX(this->X);
		this->Y = GridY(this->Y);
		int waitTime = 0;
		if(startState==SSSTATE_GROWN)
		{
			this->X=GridX(Hero->X+8);
			this->Y=GridY(Hero->Y+8);
		}
		if(startState)setSoilState(index,startState);
		bool wasRafting = false;
		if(getSoilState(index)==SSSTATE_ACTIVE) setSoilState(index,SSSTATE_GROWN);
		while(true)
		{
			int state = getSoilState(index);
			if(state!=SSSTATE_ACTIVE)
			{
				if(Hero->X>this->X-8&&Hero->X<this->X+8&&Hero->Y>this->Y-8&&Hero->Y<this->Y+8)
				{
					if(usingBugs>0&&!gskull)
					{
						usingBugs=0;
						Waitframes(120);
						int numitems = Rand(3,6);
						for(int i=0;i<numitems;i++)
						{
							int itemPick = Rand(1,100);
							int id = 0;
							if(itemPick<=33)id=0;
							else if(itemPick<=60)id=1;
							else if(itemPick<=70)id=38;
							else if(itemPick<=75)id=87;
							else if(itemPick<=85)id=73;
							else if(itemPick<=95)id=80;
							else id = 156;
							int x = Clamp(this->X+Rand(-32,32),0,240);
							int y = Clamp(this->Y+Rand(-32,32),0,160);
							item itm1 = CreateItemAt(id,x,y);
							itm1->Pickup=IP_TIMEOUT;
						}
					}
					else if(usingBugs>0)
					{
						//UNFINISHED // Spawn Gold Skulltula
						usingBugs=0;
					}
				}
			}
			if(state==SSSTATE_WAITING)
			{
				this->Data = COMBO_SOIL;
				if(beanCounter>0)
				{
					state = SSSTATE_PLANTED;
					Game->Counter[CR_MAGICBEANS]--;
					beanCounter=0;
				}
			}
			if(state==SSSTATE_PLANTED)
			{
				this->Data = COMBO_BEANSPROUT;
				if(playedSong==SONG_SongOfStorms)
				{
					state=SSSTATE_GROWN;
				}
				else
				{
					for(int i=1; i<=Screen->NumLWeapons(); i++)
					{
						lweapon l = Screen->LoadLWeapon(i);
						if((l->ID==LW_WATER||l->ID==LW_HOTWATER)&&Collision(this, l))
						{
							state = SSSTATE_GROWN;
							break;
						}
					}
				}
			}
			if(state==SSSTATE_GROWN)
			{
				this->Data = COMBO_BEANPLANT;
				if(Hero->X>this->X-8&&Hero->X<this->X+8&&Hero->Y>this->Y-8&&Hero->Y<this->Y+8)
				{
					Hero->X = this->X;
					Hero->Y = this->Y;
					//if(startState==2)Waitframe();
					Rafting[RAFT_DIR] = raftDir;
					Rafting[RAFT_UP] = screensUp;
					if(startState==SSSTATE_GROWN)
					{
						Rafting[RAFT_X] = this->X - 5*dirX(raftDir);
						Rafting[RAFT_Y] = this->Y - 5*dirY(raftDir);
					}
					else
					{
						Rafting[RAFT_X] = -1;
						Rafting[RAFT_Y] = -1;
					}
					state = SSSTATE_ACTIVE;
					canFall=false;
				}
			}
			if(state == SSSTATE_ACTIVE)
			{
				if(Hero->Action==LA_RAFTING)
				{
					if(startState==SSSTATE_GROWN)
					{
						this->Data = COMBO_INVIS;
					}
					else
					{
						this->Data = COMBO_SOIL;
					}
				}
				unless(wasRafting || Hero->Action!=LA_RAFTING) wasRafting = true;
				if(wasRafting && Hero->Action!=LA_RAFTING)
				{
					if(startState==SSSTATE_GROWN)
					{
						Quit();
					}
					else
					{
						state = SSSTATE_GROWN;
						Rafting[RAFT_DIR] = -1;
						Rafting[RAFT_UP] = -1;
					}
				}
			}
			setSoilState(index,state);
			Waitframe();
		}
	}
}

int getSoilCombo(int index)
{
	switch(getSoilState(index))
	{
		case SSSTATE_WAITING:
			return COMBO_SOIL;
		case SSSTATE_PLANTED:
			return COMBO_BEANSPROUT;
		case SSSTATE_GROWN:
			return COMBO_BEANPLANT;
	}
	return COMBO_INVIS;
}

item script MagicBean
{
	void run()
	{
		if(Game->Counter[CR_MAGICBEANS]>0)
			beanCounter=10;
	}
}

//end SoftSoil
//start ladder/vine
ffc script LadderClimb
{
	void run(int climbtime, int dir, int floorsUp, int tiledist, int allowedOffset)
	{
		if(tiledist<1)tiledist = 1;
		if(allowedOffset<1)allowedOffset=4;
		int ctime = 0;
		while(true)
		{
			while(isFrozen) Waitframe();
			if(ctime>=climbtime)
			{
				switch(dir)
				{
					case DIR_UP:
					case DIR_DOWN:
						Hero->X = this->X;
						Hero->Y+=dirY(dir)*(16*tiledist);
						break;
					case DIR_LEFT:
					case DIR_RIGHT:
						Hero->X+=dirX(dir)*(16*tiledist);
						Hero->Y = this->Y;
						
				}
				warpNumFloors=floorsUp;
			}
			switch(dir)
			{
				case DIR_UP:
				{
					if(Abs(Hero->X-this->X)<=allowedOffset && Hero->Y==this->Y)
					{
						if(Input->Button[CB_UP])ctime++;
						else ctime=0;
					}
					else ctime=0;
					break;}
				case DIR_DOWN:
				{
					if(Abs(Hero->X-this->X)<=allowedOffset && Hero->Y==this->Y)
					{
						if(Input->Button[CB_DOWN])ctime++;
						else ctime=0;
					}
					else ctime=0;
					break;}
				case DIR_LEFT:
				{
					if(Abs(Hero->Y-this->Y)<=allowedOffset && Hero->X==this->X)
					{
						if(Input->Button[CB_LEFT])ctime++;
						else ctime=0;
					}
					else ctime=0;
					break;}
				case DIR_RIGHT:
				{
					if(Abs(Hero->Y-this->Y)<=allowedOffset && Hero->X==this->X)
					{
						if(Input->Button[CB_RIGHT])ctime++;
						else ctime=0;
					}
					else ctime=0;
					break;}
			}
			if(ctime%20==1)Audio->PlaySound(SFX_LADDER_CLIMB);
			Waitframe();
		}
	}
}
//end ladder/vine
//start TorchPuzzle
//Torch puzzles!
//defNum should be a 16-bit number with the first X bits filled, where X is 16 - the total number of torches.
//thisNum should be a 16-bit number where exactly 1 bit is true, with each torch in the puzzle having a seperate bit.
//timer is the total timer for the puzzle completion.
//puzzleNum is a unique number for each puzzle
//defNum and timer and puzzleNum should be the same for all torches, thisNum should be different for every torch.
ffc script TorchPuzzle
{
	void run(int thisNum, int defNum, int unlitCombo, int litCombo, int timer, int puzzleNum, bool noSecretSFX)
	{
		int add = (4*Game->GetCurLevel());
		unless(torchPuzzleStates[puzzleNum + add])
		{
			bool lit = false;
			torchPuzzle[puzzleNum + add] |= defNum;
			if(torchPuzzle[puzzleNum + add] & thisNum)
			{
				this->Data = litCombo;
				lit = true;
			}
			else
			{
				this->Data = unlitCombo;
			}
			until(torchPuzzleStates[puzzleNum + add])
			{
				unless(lit)
				{
					if(torchLight(this))
					{
						unless(torchPuzzleTimer)torchPuzzleTimer=timer;
						lit = true;
						this->Data = litCombo;
						torchPuzzle[puzzleNum + add] |= thisNum;
					}
				}
				else
				{
					stickLight(this);
					unless(torchPuzzleTimer)
					{
						lit = false;
						this->Data = unlitCombo;
						torchPuzzle[puzzleNum + add] = defNum;
					}
					if(torchPuzzle[puzzleNum + add]==1111111111111111b)
					{
						torchPuzzleStates[puzzleNum + add]=true;
						unless(noSecretSFX)Game->PlaySound(SFX_SECRET);
					}
				}
				Waitframe();
			}
		}
		this->Data = litCombo;
		while(true)
		{
			stickLight(this);
			Waitframe();
		}
	}
}

int getTorchCombo(int torchnum, int unlitCombo, int litCombo, int puzzleNum)
{
	int add = 4 * Game->GetCurLevel();
	if(torchPuzzleStates[puzzleNum + add]) return litCombo;
	return Cond((torchPuzzle[puzzleNum + add] & torchnum) > 0,litCombo,unlitCombo);
}

ffc script LitTorch
{
	void run()
	{
		while(true)
		{
			stickLight(this);
			Waitframe();
		}
	}
}

bool torchLight(ffc torch)
{
	for(int i = 1;i<=Screen->NumLWeapons();i++)
	{
		lweapon weap = Screen->LoadLWeapon(i);
		if(weap->isValid()&&weap->ID==LW_DEKUSTICK)
		{
			unless(dekuStickLitTimer)continue;
			return Collision(torch, weap);
		}
		else if(weap->isValid()&&weap->ID==LW_ARROW&&arrowType==ARRTYPE_FIRE)
		{
			return Collision(torch, weap);
		}
	}
}

void stickLight(ffc torch)
{
	if(dekuStickLitTimer!=DEKU_MAX_TIMER)
	{
		for(int i = 1;i<=Screen->NumLWeapons();i++)
		{
			lweapon weap = Screen->LoadLWeapon(i);
			if(weap->isValid()&&weap->ID==LW_DEKUSTICK)
			{
				if(Collision(torch, weap))
				{
					dekuStickLitTimer=DEKU_MAX_TIMER;
				}
			}
			else if(weap->isValid()&&weap->ID==LW_ARROW&&!arrowType)
			{
				if(Collision(torch, weap))
				{
					arrowType=ARRTYPE_FIRE;
					weap->UseSprite(SPRITE_FIREARROW);
					if(weap->Dir==DIR_RIGHT)
					{
						weap->Tile+=1;
						weap->Flip=0;
					}
					else if(weap->Dir==DIR_LEFT)
					{
						weap->Tile+=1;
						weap->Flip=1;
					}
				}
			}
		}
	}
}

ffc script TorchSecrets
{
	void run(int numPuzzle)
	{
		this->Data=COMBO_INVIS;
		int add = 4 * Game->GetCurLevel();
		until(torchPuzzleStates[numPuzzle + add])Waitframe();
		Screen->TriggerSecrets();
	}
}

item script dekuStick
{
	void run()
	{
		for(int i = 1;i<=Screen->NumLWeapons();i++)
		{
			lweapon weap = Screen->LoadLWeapon(i);
			if(weap->isValid()&&weap->ID==LW_DEKUSTICK)
			{
				Quit();
			}
		}
		lweapon stick = CreateLWeaponAt(LW_DEKUSTICK,Hero->X+InFrontX(Hero->Dir,0),Hero->Y+InFrontY(Hero->Dir,0));
		stick->Dir = Hero->Dir;
		if(dekuStickLitTimer>0)
		{
			stick->Tile = TILE_DEKUSTICK_LIT + stick->Dir;
			stick->OriginalTile = stick->Tile;
		}
		else
		{
			stick->Tile = TILE_DEKUSTICK_UNLIT + stick->Dir;
			stick->OriginalTile = stick->Tile;
		}
		stick->Step = 0;
		stick->CollDetection = false;
		stick->DeadState = 15;
		freezeFrames+=15;
		Hero->Action = LA_ATTACKING;
	}
}
//end TorchPuzzle
//start owlStatue
int owlStates = 0000000000b;
int getOwlCombo(int index)
{
	if(owlStates&Pow(2,index)) return COMBO_OWLON;
	return COMBO_OWLOFF;
}
ffc script owlStatue//start
{
	void run(int index,bool isFake)
	{
		int trueValue = Pow(2,index);
		if((owlStates & trueValue)==trueValue)
		{
			this->Data=COMBO_OWLON;
		}
		else
		{
			this->Data=COMBO_OWLOFF;
		}
		until(isFake)
		{
			unless(form)
			{
				for(int i=countLWeaponType(LW_SWORD);i>0;--i)
				{
					lweapon weap = loadLWeaponType(LW_SWORD,i);
					if(weap->isValid()&&Collision(weap,this))
					{
						Waitframes(2);
						this->Data=COMBO_OWLON;
						owlStates|=trueValue;
						while(weap->isValid())Waitframe();
						Waitframe();
						quickSave(Game->GetCurDMapScreen(),Game->GetCurDMap());
					}
				}
			}
			Waitframe();
		}
	}
}//end
void warpToOwl(int index)//start
{
	switch(index)
	{
		case 0:
			Hero->Warp(1,0x6B);
			break;
		case 1:
			break;
		case 2:
			break;
		case 3:
			break;
		case 4:
			break;
		case 5:
			break;
		case 6:
			break;
		case 7:
			break;
		case 8:
			break;
		case 9:
			break;
	}// UNFINISHED / add more owls
}//end
void quickSave(int scrn, int dmap)//start
{
	tangoTemp=false;
	ShowMessageAndWait(43);
	if(tangoTemp)
	{
		owlSaved=true;
		Game->ContinueDMap=dmap;
		Game->ContinueScreen=scrn;
		saveTheGame();
		Game->End();
	}
}//end
//end owlStatue
//start chests
ffc script chest
{
	void run(int index, bool killAllEnemies, int FORCE_PRIZE, bool triggerSecrets)
	{
		int itemID = FORCE_PRIZE>0 ? castItem(FORCE_PRIZE) : Items::getItem(<u>index);
		if(this->Data==COMBO_INVIS||!this->Data)this->Data=COMBO_CHEST;
		int closed = this->Data;
		int open = this->Data+1;
		if(getChestState(index))
		{
			this->Data=open;
			killAllEnemies = false;
			triggerSecrets = false;
		}
		bool solid = (!(this->X%16)&&!(this->Y%16))&&!this->Flags[FFCF_TRANS];
		if(solid&&!(killAllEnemies||triggerSecrets))Screen->ComboD[ComboAt(this->X,this->Y)]=COMBO_SOLID;
		while(true)
		{
			if(getChestState(index))
			{
				this->Data=open;
				while(true)
				{
					unless(solid)ffcSolid(this);
					Waitframe();
				}
			}
			else
			{
				if(killAllEnemies)
				{
					this->Data = COMBO_INVIS;
					while(EnemiesAlive())Waitframe();
					killAllEnemies=false;
					Game->PlaySound(SFX_CHEST_APPEAR);
					this->Data=closed;
					if(solid)Screen->ComboD[ComboAt(this->X,this->Y)]=COMBO_SOLID;
				}
				if(triggerSecrets)
				{
					this->Data = COMBO_INVIS;
					until(Screen->State[ST_SECRET]) Waitframe();
					triggerSecrets=false;
					//Game->PlaySound(SFX_CHEST_APPEAR); //Should be handled by secrets sound
					this->Data=closed;
					if(solid)Screen->ComboD[ComboAt(this->X,this->Y)]=COMBO_SOLID;
				}
				unless(solid)ffcSolid(this);
				if(Input->Press[CB_A]&&AgainstComboBase(this->X,this->Y))
				{
					Input->Press[CB_A]=false;Input->Button[CB_A]=false;
					if(Hero->Item[itemID])
					{
						itemID=IT_RUP_5;
					}
					this->Data=open;
					item itm = CreateItemAt(itemID,Hero->X,Hero->Y);
					itemdata id = Game->LoadItemData(itemID);
					itm->Pickup=IP_HOLDUP;
					unless(id->PScript)
					{
						id->PScript = Game->GetItemScript("ItemPickup");
						do Waitframe(); while(Tango_AnySlotIsActive());
						id->PScript = 0;
					}
					setChestState(index,true);
				}
			}
			Waitframe();
		}
	}
	bool AgainstComboBase(int x, int y)
	{
		return Hero->Z == 0 && ((Hero->Dir == DIR_UP && Hero->Y == y+8 && Abs(Hero->X-x) < 8)||(Hero->Dir == DIR_DOWN && Hero->Y == y-16 && Abs(Hero->X-x) < 8)||(Hero->Dir == DIR_LEFT && Hero->X == x+16 && Abs(Hero->Y-y) < 8)||(Hero->Dir == DIR_RIGHT && Hero->X == x-16 && Abs(Hero->Y-y) < 8));
	}
}

int getChestCombo(int data, int index)
{
	if(data == COMBO_INVIS || data == 0)data = COMBO_CHEST;
	if(getChestState(index))++data;
	return data;
}
//end chests
//start Mirror
ffc script Mirror
{
	void run(int index, int clockdir, int relativeHeight)
	{
		DEFINE NUM_VARS = 9;
		DEFINE INITIALIZED = index * NUM_VARS;
		DEFINE DIR = INITIALIZED + 1;
		DEFINE FLX = INITIALIZED + 2;
		DEFINE FLY = INITIALIZED + 3;
		DEFINE FRX = INITIALIZED + 4;
		DEFINE FRY = INITIALIZED + 5;
		DEFINE DIRX = INITIALIZED + 6;
		DEFINE DIRY = INITIALIZED + 7;
		DEFINE CHARGETIME = INITIALIZED + 8;
		
		unless(screenData[INITIALIZED])
		{
			screenData[DIR] = AngleDir8(WrapDegrees(-90 + (45*clockdir)));
			switch(screenData[DIR])
			{
				case DIR_UP:
					screenData[FLX] = this->X;
					screenData[FRX] = this->X + 15;
					screenData[FLY] = this->Y;
					screenData[FRY] = this->Y;
					break;
				case DIR_DOWN:
					screenData[FLX] = this->X + 15;
					screenData[FRX] = this->X;
					screenData[FLY] = this->Y + 15;
					screenData[FRY] = this->Y + 15;
					break;
				case DIR_RIGHT:
					screenData[FLX] = this->X + 15;
					screenData[FRX] = this->X + 15;
					screenData[FLY] = this->Y;
					screenData[FRY] = this->Y + 15;
					break;
				case DIR_LEFT:
					screenData[FLX] = this->X;
					screenData[FRX] = this->X;
					screenData[FLY] = this->Y + 15;
					screenData[FRY] = this->Y;
					break;
				case DIR_RIGHTDOWN:
					screenData[FLX] = this->X + 15 + 8;
					screenData[FRX] = this->X + 8;
					screenData[FLY] = this->Y + 8;
					screenData[FRY] = this->Y + 15 + 8;
					break;
				case DIR_RIGHTUP:
					screenData[FLX] = this->X + 8;
					screenData[FRX] = this->X + 15 + 8;
					screenData[FLY] = this->Y - 8;
					screenData[FRY] = this->Y + 8;
					break;
				case DIR_LEFTDOWN:
					screenData[FLX] = this->X + 8;
					screenData[FRX] = this->X - 8;
					screenData[FLY] = this->Y + 15 + 8;
					screenData[FRY] = this->Y + 8;
					break;
				case DIR_LEFTUP:
					screenData[FLX] = this->X - 8;
					screenData[FRX] = this->X + 8;
					screenData[FLY] = this->Y + 8;
					screenData[FRY] = this->Y - 8;
					break;
			}
			screenData[DIRX] = dirX(screenData[DIR]);
			screenData[DIRY] = dirY(screenData[DIR]);
			screenData[INITIALIZED] = 1;
		}
	}
}

int screenData[2048]; //cleared when screen changes, NOT for height change though!
//end Mirror
//start Pots
DEFINE POT_STEP_MOVING = 75;
DEFINE POT_STEP_STILL = 150;
ffc script Pot
{
	void run(ItemID drop, int underCombo)
	{
		if(drop<0) drop = getDropsetDrop(DROPSET_GRASS);
		unless(underCombo) underCombo = COMBO_INVIS;
		combodata cd = Game->LoadComboData(this->Data);
		bool dodrop = drop>-1;
		while(true)
		{
			for(int q = Screen->NumLWeapons(); q>=MIN_LWEAPON; --q)
			{
				lweapon weap = Screen->LoadLWeapon(q);
				switch(weap->ID)
				{
					case LW_SWORD:
					case LW_BOMBBLAST:
					case LW_BRANG:
						if(Collision(this,weap))
						{
							Audio->PlaySound(SFX_POT_SHATTER);
							item shards = CreateItemAt(IT_DUMMY,this->X,this->Y);
							shards->Pickup = IP_DUMMY | IP_TIMEOUT;
							shards->OriginalTile = cd->Tile + 20;
							shards->Tile = cd->Tile + 20;
							shards->NumFrames = cd->Frames;
							shards->CSet = cd->CSet;
							if(dodrop)
							{
								item it = CreateItemAt(drop, this->X, this->Y);
								it->Pickup = IP_TIMEOUT;
							}
							this->Data = underCombo;
							Quit();
						}
				}
			}
			if(AgainstFFCBase(this) && HoldingObject == HOLD_NONE)
			{
				setGlobalBool(GVAR_AGAINST_PICKUP,GV_MISC_BOOL,true);
				if(Input->Press[CB_R])
					break;
			}
			ffcSolid(this);
			Waitframe();
		}
		//Picked up
		Audio->PlaySound(SFX_PICKUP_OBJ);
		HoldingObject = HOLD_POT;
		item it = CreateItemAt(IT_DUMMY,Hero->X,Hero->Y-8);
		it->Pickup = IP_DUMMY;
		it->OriginalTile = cd->Tile;
		it->Tile = cd->Tile;
		it->NumFrames = cd->Frames;
		it->CSet = cd->CSet;
		it->Script = Game->GetItemSpriteScript("flyingPot");
		it->InitD[0] = drop;
		this->Data = underCombo;
		TotalNoAction();
	}
}

itemsprite script flyingPot
{
	void run(ItemID dropItem)
	{
		bool dodrop = dropItem>-1;
		until(Input->Press[CB_A]||Input->Press[CB_B]||Input->Press[CB_EX1]||Input->Press[CB_EX2]||Input->Press[CB_R]||Input->Press[CB_L])
		{
			this->Misc[ITEMMISC_LAYERDMAP] = Game->GetCurDMap() + 1;
			this->X = Hero->X;
			this->Y = Hero->Y - 8;
			Waitframe();
		}
		TotalNoAction();
		mapdata map = getLayerMapdata(getRelativeHeight(this));
		this->X = Hero->X;
		this->Y = Hero->Y;
		HoldingObject = HOLD_NONE;
		int dir = HeroDir8,
		    pix = Hero->Action==LA_WALKING ? (POT_STEP_MOVING/100) + (1.5+LinkMovement_GetLinkSpeedBoost()) : POT_STEP_STILL/100;
		for(int q = 0; q < 60; ++q)
		{
			while(true)
			{
				int centerCombo = ComboAt(this->X + InFrontCenteredX(dir,pix),this->Y + InFrontCenteredY(dir,pix));
				if(map->ComboI[centerCombo] == CI_RAMP)
				{
					if(map->ComboF[centerCombo] == CF_RAMP_UP)
					{
						++this->Misc[ITEMMISC_LAYERDMAP];
						map = getLayerMapdata(getRelativeHeight(this));
						continue;
					}
				}
				break;
			}
			unless(MM2D::CanWalkM(this, map, dir, Ceiling(pix),true))
				break;
			if(offscreen(this->X + (dirX(dir) * pix), this->Y + (dirY(dir) * pix)))
				break;
			this->X += dirX(dir) * pix;
			this->Y += dirY(dir) * pix;
			Waitframe();
		}
		Audio->PlaySound(SFX_POT_SHATTER);
		this->OriginalTile += 20; //shatter
		//Fall down pits
		for(int x = this->X+3; x<this->X+13; x+=9)
		{
			for(int y = this->Y+9; y<this->Y+13; y+=3)
			{
				if(map->ComboT[ComboAt(x,y)] == CT_PITFALL)
				{
					--this->Misc[ITEMMISC_LAYERDMAP];
					//Reset loop to re-check next layer:
					x = this->X+3;
					y = this->Y+9;
					map = getLayerMapdata(getRelativeHeight(this));
				}
			}
		}
		if(dodrop)
		{
			item it = CreateItemAt(dropItem,this->X,this->Y);
			it->Misc[ITEMMISC_LAYERDMAP] = this->Misc[ITEMMISC_LAYERDMAP];
		}
		this->Pickup |= IP_TIMEOUT;
		Quit();
	}
}
//end
//start BalloonTarget
ffc script BalloonTarget
{
	void run(int index, bool solid)
	{
		until(getGlobalBool(index,GV_ONETIME_TARGETS_RESETTABLE))
		{
			if(solid)ffcSolid(this);
			Waitframe();
			for(int q = Screen->NumLWeapons(); q >= MIN_LWEAPON; --q)
			{
				lweapon weap = Screen->LoadLWeapon(q);
				switch(weap->ID)
				{
					case LW_MAGIC:
					case LW_ARROW:
					{
						if(HeightCollision(this,weap))
						{
							Audio->PlaySound(SFX_BALLOON_POP);
							setGlobalBool(index,GV_ONETIME_TARGETS_RESETTABLE,true);
						}
					}
				}
			}
		}
		this->Data = COMBO_INVIS;
	}
}
//end
